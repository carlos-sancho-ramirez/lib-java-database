apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

group = 'com.github.carlos-sancho-ramirez'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.carlos-sancho-ramirez:lib-java-collections:1.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'lib-java-database'
            from components.java

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath

  options {
    source '8'
    setMemberLevel JavadocMemberLevel.PUBLIC
    setAuthor false

    links "https://docs.oracle.com/javase/8/docs/api/"
  }
}
